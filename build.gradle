buildscript {
	repositories {
		mavenCentral()
		maven { url "https://jitpack.io" }
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:2.1.0'
		classpath 'com.github.alxndrsn:android-check:1f87548c0686d823e5f1490a2053b70bd9f5022e'
		classpath 'com.trickyandroid:jacoco-everywhere:0.2.1'
	}
	configurations.all {
		resolutionStrategy {
			// use old jacoco so Robolectric test coverage is included in reports
			// track https://github.com/paveldudka/JacocoEverywhere/issues/14 for a less hacky solution
			force 'org.jacoco:org.jacoco.core:0.7.2.201409121644'
		}
	}
}
apply plugin: 'com.android.application'
apply plugin: 'com.noveogroup.android.check'
apply plugin: 'jacoco-everywhere'

// enable verbose lint warnings
gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << '-Xlint:deprecation'
	}
}

repositories {
	mavenCentral()
	maven { url 'https://download.01.org/crosswalk/releases/crosswalk/android/maven2' }
	jcenter()
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	compile 'com.android.support:appcompat-v7:21.0.3'
	compile 'org.xwalk:xwalk_core_library:23.53.589.4:arm@aar'
	compile 'com.simprints:LibSimprints:1.0.8'
}

def getVersionCode = {
	if(System.env.JENKINS == 'true') {
		def versionParts = System.env.VERSION_TO_BUILD.split(/\./)
		if(versionParts.length != 3)
			throw new RuntimeException("Unexpected version number - should be of form 'x.y.z', but was: $System.env.VERSION_TO_BUILD")
		versionParts = versionParts.collect { Integer.parseInt(it) }
		if(versionParts[1] > 999 || versionParts[2] > 999)
			throw new RuntimeException('Version part greater than 999 not allowed.')
		int versionCode = (1000000 * versionParts[0]) +
				  (1000 * versionParts[1]) +
				  versionParts[2]
		if(versionCode > 2100000000)
			throw new RuntimeException('versionCode biggger than max allowed by Google Play.')
		return versionCode
	}
	return 1
}
def getVersionName = {
	System.env.VERSION_TO_BUILD ?: 'SNAPSHOT'
}

task coverageReport(dependsOn: 'createUnbrandedDebugCoverageReport')
coverageReport.doLast {
	// Add CLI report for Jacoco coverage
	def slurper = new XmlSlurper()
	slurper.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false) 
	slurper.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false);

	['unbranded'].each { flavour ->
		def report = slurper.parse("./build/reports/coverage/${flavour}/debug/report.xml")

		println "------ Code Coverage (flavour: $flavour)) ------"
		report.package.each { p ->
			println "--> ${p.@name.toString().replaceAll('/', '.')}"
			p.counter.each { c ->
				def covered = c.@covered.toString() as int
				def missed = c.@missed.toString() as int
				def percent = ((100 * covered) / (covered + missed))
				println String.format('    %-16s %.1f ', c.@type, percent) + '%'
			}
			println "----------------------------------"
		}
	}
}

android {
	compileSdkVersion 22
	buildToolsVersion '23.0.3'
	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
	}

	defaultConfig {
		versionCode getVersionCode()
		versionName getVersionName()
		archivesBaseName = "${project.name}-${versionName}"
	}

	applicationVariants.all {
		buildConfigField "boolean", "DISABLE_APP_URL_VALIDATION", "Boolean.parseBoolean(\"${System.env.DISABLE_APP_URL_VALIDATION}\")";
		buildConfigField "String", "LOG_TAG", '"MedicMobile"'
		try {
			buildConfigField "String", "SIMPRINTS_API_KEY", /"$SIMPRINTS_API_KEY"/
		} catch(_) {
			buildConfigField "String", "SIMPRINTS_API_KEY", /"Medic's API Key"/
		}
		try {
			buildConfigField "String", "SIMPRINTS_USER_ID", /"$SIMPRINTS_USER_ID"/
		} catch(_) {
			buildConfigField "String", "SIMPRINTS_USER_ID", '"some-user-id"'
		}
		try {
			buildConfigField "String", "SIMPRINTS_MODULE_ID", /"$SIMPRINTS_MODULE_ID"/
		} catch(_) {
			buildConfigField "String", "SIMPRINTS_MODULE_ID", '"Medic Module ID"'
		}
	}

	signingConfigs {
		release {
			storeFile file(System.env.ANDROID_KEYSTORE_PATH ?: signingConfigs.debug.storeFile)
			storePassword System.env.ANDROID_KEYSTORE_PASSWORD ?: signingConfigs.debug.storePassword
			keyAlias System.env.ANDROID_KEY_ALIAS ?: signingConfigs.debug.keyAlias
			keyPassword System.env.ANDROID_KEY_PASSWORD ?: signingConfigs.debug.keyPassword
		}
	}

	buildTypes {
		debug {
			testCoverageEnabled = true
		}
		release {
			minifyEnabled false
			proguardFile getDefaultProguardFile('proguard-android.txt')
			signingConfig signingConfigs.release
		}
	}

	check {
		abortOnError true
	}

	lintOptions {
		disable 'UnusedResources' // linter can't handle static imports, so just skip this test
		disable 'GradleDependency' // TODO update to latest support-v4 lib and re-enable this rule

		warningsAsErrors true

		xmlReport false

		if(System.env.TRAVIS == 'true') {
			abortOnError true
			htmlReport false
			textReport true
			textOutput 'stdout'
		}
	}

	productFlavors {
		unbranded {
			// we will not create project-specific src directories
			// for `unbranded` - it will use the defaults in
			// src/main
		}
		medicmobiledemo {
			applicationId = 'org.medicmobile.webapp.mobile.medicmobiledemo'
		}

		bracuganda {
			applicationId = 'org.medicmobile.webapp.mobile.bracuganda'
		}
		ebpp_indonesia {
			applicationId = 'org.medicmobile.webapp.mobile.ebpp_indonesia'
		}
		livinggoods {
			applicationId = 'org.medicmobile.webapp.mobile.livinggoods'
		}
		livinggoodskenya {
			applicationId = 'org.medicmobile.webapp.mobile.livinggoodskenya'
		}
		musomali {
			applicationId = 'org.medicmobile.webapp.mobile.musomali'
		}
		simprints {
			applicationId = 'org.medicmobile.webapp.mobile.simprints'
		}
	}
}
